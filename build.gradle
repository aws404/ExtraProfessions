plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = System.getenv("VERSION") ? System.getenv("VERSION") : "unversioned"

repositories {
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven { url = "https://jitpack.io" }
}

dependencies {
	// Fabric Dependencies
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Mod Dependencies
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation include("maven.modrinth:sjvt:${project.sjvt_version}")
	modImplementation include("maven.modrinth:booking_it:${project.bi_version}")
	modImplementation include("com.github.Chocohead:Fabric-ASM:v${project.mm_version}") {
		exclude (group: "net.fabricmc.fabric-api")
	}

	// Development Environment Mods
	modImplementation "maven.modrinth:here-be-no-dragons:1.0.0"
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}

loom {
	accessWidenerPath = file("src/main/resources/extra_professions.accesswidener")
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = project.project_id
	versionNumber = version
	versionName = System.getenv("VERSION_NAME") ? System.getenv("VERSION_NAME") : version
	versionType = System.getenv("ALPHA") ? "alpha" : "release"
	changelog = System.getenv("CAHNGELOG")
	uploadFile = remapJar
	dependencies {
		required.project "P7dR8mSH" // Mark Fabric API as a required dependency
	}
}